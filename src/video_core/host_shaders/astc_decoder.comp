// SPDX-FileCopyrightText: Copyright 2021 yuzu Emulator Project
// SPDX-License-Identifier: GPL-2.0-or-later

#version 450

#ifdef VULKAN

#define BEGIN_PUSH_CONSTANTS layout(push_constant) uniform PushConstants {
#define END_PUSH_CONSTANTS };
#define UNIFORM(n)
#define BINDING_INPUT_BUFFER 0
#define BINDING_OUTPUT_IMAGE 1

#else // ^^^ Vulkan ^^^ // vvv OpenGL vvv

#define BEGIN_PUSH_CONSTANTS
#define END_PUSH_CONSTANTS
#define UNIFORM(n) layout(location = n) uniform
#define BINDING_INPUT_BUFFER 0
#define BINDING_OUTPUT_IMAGE 0

#endif

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

BEGIN_PUSH_CONSTANTS
UNIFORM(1) uvec2 block_dims;
UNIFORM(2) uint layer_stride;
UNIFORM(3) uint block_size;
UNIFORM(4) uint x_shift;
UNIFORM(5) uint block_height;
UNIFORM(6) uint block_height_mask;
END_PUSH_CONSTANTS

struct EncodingData {
    uint data;
};

struct PartitionTable {
    uint s1, s2, s3, s4, s5, s6, s7, s8;
    uint rnum;
    bool small_block;
};

layout(binding = BINDING_INPUT_BUFFER, std430) readonly restrict buffer InputBufferU32 {
    uvec4 astc_data[];
};

layout(binding = BINDING_OUTPUT_IMAGE, rgba8) uniform writeonly restrict image2DArray dest_image;

const uint GOB_SIZE_X_SHIFT = 6;
const uint GOB_SIZE_Y_SHIFT = 3;
const uint GOB_SIZE_SHIFT = GOB_SIZE_X_SHIFT + GOB_SIZE_Y_SHIFT;

const uint BYTES_PER_BLOCK_LOG2 = 4;

const uint JUST_BITS = 0u;
const uint QUINT = 1u;
const uint TRIT = 2u;

// ASTC Encodings data, sorted in ascending order based on their BitLength value
// (see GetBitLength() function)
const uint encoding_values[22] = uint[](
    (JUST_BITS), (JUST_BITS | (1u << 8u)), (TRIT), (JUST_BITS | (2u << 8u)),
    (QUINT), (TRIT | (1u << 8u)), (JUST_BITS | (3u << 8u)), (QUINT | (1u << 8u)),
    (TRIT | (2u << 8u)), (JUST_BITS | (4u << 8u)), (QUINT | (2u << 8u)), (TRIT | (3u << 8u)),
    (JUST_BITS | (5u << 8u)), (QUINT | (3u << 8u)), (TRIT | (4u << 8u)), (JUST_BITS | (6u << 8u)),
    (QUINT | (4u << 8u)), (TRIT | (5u << 8u)), (JUST_BITS | (7u << 8u)), (QUINT | (5u << 8u)),
    (TRIT | (6u << 8u)), (JUST_BITS | (8u << 8u)));

// Shared memory for workgroup processing
shared uvec4 local_buff;
shared int total_bitsread;

// Color data globals
shared uvec4 color_endpoint_data;
shared int color_bitsread;

// Global "vector" to be pushed into when decoding
#define DIVCEIL(number, divisor) (number + divisor - 1) / divisor
#define ARRAY_NUM_ELEMENTS 144
#define VECTOR_ARRAY_SIZE DIVCEIL(ARRAY_NUM_ELEMENTS * 2, 4)
shared uint result_vector[ARRAY_NUM_ELEMENTS * 2];

shared int result_index;
shared uint result_vector_max_index;
shared bool result_limit_reached;

// avoid intermediate result_vector storage during color decode phase
shared bool write_color_values;
shared uint color_values_direct[32];
shared uint color_out_index;
shared uint color_num_values;

// Shared variables for DecompressBlock interthread communication
shared uvec4 endpoints0[4];
shared uvec4 endpoints1[4];
shared PartitionTable pt;
shared uvec2 size_params;
shared uint num_partitions;
shared uint partition_index;
shared uint plane_index;
shared bool dual_plane;
shared vec4 fill_color;

// EncodingData helpers
uint Encoding(EncodingData val) {
    return bitfieldExtract(val.data, 0, 8);
}
uint NumBits(EncodingData val) {
    return bitfieldExtract(val.data, 8, 8);
}
uint BitValue(EncodingData val) {
    return bitfieldExtract(val.data, 16, 8);
}
uint QuintTritValue(EncodingData val) {
    return bitfieldExtract(val.data, 24, 8);
}

void Encoding(inout EncodingData val, uint v) {
    val.data = bitfieldInsert(val.data, v, 0, 8);
}
void NumBits(inout EncodingData val, uint v) {
    val.data = bitfieldInsert(val.data, v, 8, 8);
}
void BitValue(inout EncodingData val, uint v) {
    val.data = bitfieldInsert(val.data, v, 16, 8);
}
void QuintTritValue(inout EncodingData val, uint v) {
    val.data = bitfieldInsert(val.data, v, 24, 8);
}

EncodingData CreateEncodingData(uint encoding, uint num_bits, uint bit_val, uint quint_trit_val) {
    return EncodingData(((encoding) << 0u) | ((num_bits) << 8u) |
                        ((bit_val) << 16u) | ((quint_trit_val) << 24u));
}
uint ReplicateBitTo9(uint bit);
uint FastReplicateTo8(uint value, uint num_bits);

void EmitColorValue(EncodingData val) {
    // write directly to color_values_direct[]
    const uint encoding = Encoding(val);
    const uint bitlen = NumBits(val);
    const uint bitval = BitValue(val);

    if (encoding == JUST_BITS) {
        color_values_direct[++color_out_index] = FastReplicateTo8(bitval, bitlen);
        return;
    }

    uint A = ReplicateBitTo9((bitval & 1));
    uint B = 0, C = 0, D = QuintTritValue(val);

    if (encoding == TRIT) {
        switch (bitlen) {
            case 1:
            C = 204;
            break;
            case 2: {
                C = 93;
                const uint b = (bitval >> 1) & 1;
                B = (b << 8) | (b << 4) | (b << 2) | (b << 1);
                break;
            }
            case 3: {
                C = 44;
                const uint cb = (bitval >> 1) & 3;
                B = (cb << 7) | (cb << 2) | cb;
                break;
            }
            case 4: {
                C = 22;
                const uint dcb = (bitval >> 1) & 7;
                B = (dcb << 6) | dcb;
                break;
            }
            case 5: {
                C = 11;
                const uint edcb = (bitval >> 1) & 0xF;
                B = (edcb << 5) | (edcb >> 2);
                break;
            }
            case 6: {
                C = 5;
                const uint fedcb = (bitval >> 1) & 0x1F;
                B = (fedcb << 4) | (fedcb >> 4);
                break;
            }
        }
    } else { // QUINT
        switch (bitlen) {
            case 1:
            C = 113;
            break;
            case 2: {
                C = 54;
                const uint b = (bitval >> 1) & 1;
                B = (b << 8) | (b << 3) | (b << 2);
                break;
            }
            case 3: {
                C = 26;
                const uint cb = (bitval >> 1) & 3;
                B = (cb << 7) | (cb << 1) | (cb >> 1);
                break;
            }
            case 4: {
                C = 13;
                const uint dcb = (bitval >> 1) & 7;
                B = (dcb << 6) | (dcb >> 1);
                break;
            }
            case 5: {
                C = 6;
                const uint edcb = (bitval >> 1) & 0xF;
                B = (edcb << 5) | (edcb >> 3);
                break;
            }
        }
    }

    uint T = (D * C) + B;
    T ^= A;
    T = (A & 0x80) | (T >> 2);
    color_values_direct[++color_out_index] = T;
}



void ResultEmplaceBack(EncodingData val) {
    if (write_color_values) {
        if (color_out_index >= color_num_values) {
            // avoid decoding more than needed by this phase
            result_limit_reached = true;
            return;
        }
        EmitColorValue(val);
        return;
    }

    if (result_index >= result_vector_max_index) {
        // Alert callers to avoid decoding more than needed by this phase
        result_limit_reached = true;
        return;
    }
    result_vector[result_index] = val.data;
    ++result_index;
}

uvec4 ReplicateByteTo16(uvec4 value) {
    return value * 0x101;
}

uint ReplicateBitTo7(uint value) {
    return value * 127;
}

uint ReplicateBitTo9(uint value) {
    return value * 511;
}

uint ReplicateBits(uint value, uint num_bits, uint to_bit) {
    if (value == 0 || num_bits == 0) {
        return 0;
    }
    if (num_bits >= to_bit) {
        return value;
    }
    const uint v = value & uint((1 << num_bits) - 1);
    uint res = v;
    uint reslen = num_bits;
    while (reslen < to_bit) {
        const uint num_dst_bits_to_shift_up = min(num_bits, to_bit - reslen);
        const uint num_src_bits_to_shift_down = num_bits - num_dst_bits_to_shift_up;

        res <<= num_dst_bits_to_shift_up;
        res |= (v >> num_src_bits_to_shift_down);
        reslen += num_bits;
    }
    return res;
}

uint FastReplicateTo8(uint value, uint num_bits) {
    return ReplicateBits(value, num_bits, 8);
}

uint FastReplicateTo6(uint value, uint num_bits) {
    return ReplicateBits(value, num_bits, 6);
}

uint Div3Floor(uint v) {
    return (v * 0x5556) >> 16;
}

uint Div3Ceil(uint v) {
    return Div3Floor(v + 2);
}

uint Div5Floor(uint v) {
    return (v * 0x3334) >> 16;
}

uint Div5Ceil(uint v) {
    return Div5Floor(v + 4);
}

uint Hash52(uint p) {
    p ^= p >> 15;
    p -= p << 17;
    p += p << 7;
    p += p << 4;
    p ^= p >> 5;
    p += p << 16;
    p ^= p >> 7;
    p ^= p >> 3;
    p ^= p << 6;
    p ^= p >> 17;
    return p;
}


PartitionTable GetPartitionTable(uint seed, uint partition_count) {
    PartitionTable pt;
    pt.small_block = (block_dims.y * block_dims.x) < 32;

    seed += (partition_count - 1) * 1024;
    uint rnum = Hash52(uint(seed));
    pt.rnum = rnum;

    uint seed1 = (rnum & 0xF);
    seed1 *= seed1;
    uint seed2 = (rnum >> 4) & 0xF;
    seed2 *= seed2;
    uint seed3 = (rnum >> 8) & 0xF;
    seed3 *= seed3;
    uint seed4 = (rnum >> 12) & 0xF;
    seed4 *= seed4;
    uint seed5 = (rnum >> 16) & 0xF;
    seed5 *= seed5;
    uint seed6 = (rnum >> 20) & 0xF;
    seed6 *= seed6;
    uint seed7 = (rnum >> 24) & 0xF;
    seed7 *= seed7;
    uint seed8 = (rnum >> 28) & 0xF;
    seed8 *= seed8;

    uint sh1, sh2;
    if ((seed & 1) > 0) {
        sh1 = (seed & 2) > 0 ? 4 : 5;
        sh2 = (partition_count == 3) ? 6 : 5;
    } else {
        sh1 = (partition_count == 3) ? 6 : 5;
        sh2 = (seed & 2) > 0 ? 4 : 5;
    }

    pt.s1 = seed1 >> sh1;
    pt.s2 = seed2 >> sh2;
    pt.s3 = seed3 >> sh1;
    pt.s4 = seed4 >> sh2;
    pt.s5 = seed5 >> sh1;
    pt.s6 = seed6 >> sh2;
    pt.s7 = seed7 >> sh1;
    pt.s8 = seed8 >> sh2;

    return pt;
    }

uint SelectPartition(PartitionTable pt, uint x, uint y, uint partition_count) {
    if (pt.small_block) {
        x <<= 1;
        y <<= 1;
    }

    uint a = pt.s1 * x + pt.s2 * y + (pt.rnum >> 14);
    uint b = pt.s3 * x + pt.s4 * y + (pt.rnum >> 10);
    uint c = pt.s5 * x + pt.s6 * y + (pt.rnum >> 6);
    uint d = pt.s7 * x + pt.s8 * y + (pt.rnum >> 2);

    a &= 0x3F;
    b &= 0x3F;
    c &= 0x3F;
    d &= 0x3F;

    if (partition_count < 4) d = 0;
    if (partition_count < 3) c = 0;

    if (a >= b && a >= c && a >= d) {
        return 0;
    } else if (b >= c && b >= d) {
        return 1;
    } else if (c >= d) {
        return 2;
    } else {
        return 3;
    }
}

uint ExtractBits(uvec4 payload, int offset, int bits) {
    if (bits <= 0) {
        return 0;
    }
    if (bits > 32) {
        return 0;
    }
    const int last_offset = offset + bits - 1;
    const int shifted_offset = offset >> 5;
    if ((last_offset >> 5) == shifted_offset) {
        return bitfieldExtract(payload[shifted_offset], offset & 31, bits);
    }
    const int first_bits = 32 - (offset & 31);
    const int result_first = int(bitfieldExtract(payload[shifted_offset], offset & 31, first_bits));
    const int result_second = int(bitfieldExtract(payload[shifted_offset + 1], 0, bits - first_bits));
    return result_first | (result_second << first_bits);
}

uint StreamBits(uint num_bits) {
    const int int_bits = int(num_bits);
    const uint ret = ExtractBits(local_buff, total_bitsread, int_bits);
    total_bitsread += int_bits;
    return ret;
}

void SkipBits(uint num_bits) {
    const int int_bits = int(num_bits);
    total_bitsread += int_bits;
}

uint StreamColorBits(uint num_bits) {
    const int int_bits = int(num_bits);
    const uint ret = ExtractBits(color_endpoint_data, color_bitsread, int_bits);
    color_bitsread += int_bits;
    return ret;
}

EncodingData GetEncodingFromVector(uint index) {
    const uint data = result_vector[index];
    return EncodingData(data);
}

// Returns the number of bits required to encode n_vals values.
uint GetBitLength(uint n_vals, uint encoding_index) {
    const EncodingData encoding_value = EncodingData(encoding_values[encoding_index]);
    const uint encoding = Encoding(encoding_value);
    uint total_bits = NumBits(encoding_value) * n_vals;
    if (encoding == TRIT) {
        total_bits += Div5Ceil(n_vals * 8);
    } else if (encoding == QUINT) {
        total_bits += Div3Ceil(n_vals * 7);
    }
    return total_bits;
}

uint GetNumWeightValues(uvec2 size, bool dual_plane) {
    uint n_vals = size.x * size.y;
    if (dual_plane) {
        n_vals *= 2;
    }
    return n_vals;
}

uint GetPackedBitSize(uvec2 size, bool dual_plane, uint max_weight) {
    const uint n_vals = GetNumWeightValues(size, dual_plane);
    return GetBitLength(n_vals, max_weight);
}

uint BitsBracket(uint bits, uint pos) {
    return ((bits >> pos) & 1);
}

uint BitsOp(uint bits, uint start, uint end) {
    const uint mask = (1 << (end - start + 1)) - 1;
    return ((bits >> start) & mask);
}

void DecodeQuintBlock(uint num_bits) {
    uvec3 m;
    uvec4 qQ;
    m[0] = StreamColorBits(num_bits);
    qQ.w = StreamColorBits(3);
    m[1] = StreamColorBits(num_bits);
    qQ.w |= StreamColorBits(2) << 3;
    m[2] = StreamColorBits(num_bits);
    qQ.w |= StreamColorBits(2) << 5;
    if (BitsOp(qQ.w, 1, 2) == 3 && BitsOp(qQ.w, 5, 6) == 0) {
        qQ.x = 4;
        qQ.y = 4;
        qQ.z = (BitsBracket(qQ.w, 0) << 2) | ((BitsBracket(qQ.w, 4) & ~BitsBracket(qQ.w, 0)) << 1) |
              (BitsBracket(qQ.w, 3) & ~BitsBracket(qQ.w, 0));
    } else {
        uint C = 0;
        if (BitsOp(qQ.w, 1, 2) == 3) {
            qQ.z = 4;
            C = (BitsOp(qQ.w, 3, 4) << 3) | ((~BitsOp(qQ.w, 5, 6) & 3) << 1) | BitsBracket(qQ.w, 0);
        } else {
            qQ.z = BitsOp(qQ.w, 5, 6);
            C = BitsOp(qQ.w, 0, 4);
        }
        if (BitsOp(C, 0, 2) == 5) {
            qQ.y = 4;
            qQ.x = BitsOp(C, 3, 4);
        } else {
            qQ.y = BitsOp(C, 3, 4);
            qQ.x = BitsOp(C, 0, 2);
        }
    }
    for (uint i = 0; i < 3; i++) {
        const EncodingData val = CreateEncodingData(QUINT, num_bits, m[i], qQ[i]);
        ResultEmplaceBack(val);
    }
}

void DecodeTritBlock(uint num_bits) {
    uvec4 m;
    uvec4 t;
    uvec3 Tm5t5;
    m[0] = StreamColorBits(num_bits);
    Tm5t5.x = StreamColorBits(2);
    m[1] = StreamColorBits(num_bits);
    Tm5t5.x |= StreamColorBits(2) << 2;
    m[2] = StreamColorBits(num_bits);
    Tm5t5.x |= StreamColorBits(1) << 4;
    m[3] = StreamColorBits(num_bits);
    Tm5t5.x |= StreamColorBits(2) << 5;
    Tm5t5.y = StreamColorBits(num_bits);
    Tm5t5.x |= StreamColorBits(1) << 7;
    uint C = 0;
    if (BitsOp(Tm5t5.x, 2, 4) == 7) {
        C = (BitsOp(Tm5t5.x, 5, 7) << 2) | BitsOp(Tm5t5.x, 0, 1);
        Tm5t5.z = 2;
        t[3] = 2;
    } else {
        C = BitsOp(Tm5t5.x, 0, 4);
        if (BitsOp(Tm5t5.x, 5, 6) == 3) {
            Tm5t5.z = 2;
            t[3] = BitsBracket(Tm5t5.x, 7);
        } else {
            Tm5t5.z = BitsBracket(Tm5t5.x, 7);
            t[3] = BitsOp(Tm5t5.x, 5, 6);
        }
    }
    if (BitsOp(C, 0, 1) == 3) {
        t[2] = 2;
        t[1] = BitsBracket(C, 4);
        t[0] = (BitsBracket(C, 3) << 1) | (BitsBracket(C, 2) & ~BitsBracket(C, 3));
    } else if (BitsOp(C, 2, 3) == 3) {
        t[2] = 2;
        t[1] = 2;
        t[0] = BitsOp(C, 0, 1);
    } else {
        t[2] = BitsBracket(C, 4);
        t[1] = BitsOp(C, 2, 3);
        t[0] = (BitsBracket(C, 1) << 1) | (BitsBracket(C, 0) & ~BitsBracket(C, 1));
    }
    for (uint i = 0; i < 4; i++) {
        const EncodingData val = CreateEncodingData(TRIT, num_bits, m[i], t[i]);
        ResultEmplaceBack(val);
    }
    const EncodingData val = CreateEncodingData(TRIT, num_bits, Tm5t5.y, Tm5t5.z);
    ResultEmplaceBack(val);
}

void DecodeIntegerSequence(uint max_range, uint num_values) {
    EncodingData val = EncodingData(encoding_values[max_range]);
    const uint encoding = Encoding(val);
    const uint num_bits = NumBits(val);
    uint vals_decoded = 0;
    while (vals_decoded < num_values && !result_limit_reached) {
        switch (encoding) {
        case QUINT:
            DecodeQuintBlock(num_bits);
            vals_decoded += 3;
            break;
        case TRIT:
            DecodeTritBlock(num_bits);
            vals_decoded += 5;
            break;
        case JUST_BITS:
            BitValue(val, StreamColorBits(num_bits));
            ResultEmplaceBack(val);
            vals_decoded++;
            break;
        }
    }
}

void DecodeColorValues(uvec4 modes, uint num_partitions, uint color_data_bits) {
    uint num_values = 0;
    for (uint i = 0; i < num_partitions; i++) {
        num_values += ((modes[i] >> 2) + 1) << 1;
    }
    // Find the largest encoding that's within color_data_bits
    // TODO(ameerj): profile with binary search
    int range = 0;
    while (++range < encoding_values.length()) {
        const uint bit_length = GetBitLength(num_values, range);
        if (bit_length > color_data_bits) {
            break;
        }
    }
    // Decode directly into color_values_direct[]
    write_color_values = true;
    color_out_index = 0;
    color_num_values = num_values;
    for (uint i = 0; i < 32; ++i) {
        color_values_direct[i] = 0;
    }

    DecodeIntegerSequence(range - 1, num_values);

    write_color_values = false;
}



ivec2 BitTransferSigned(int a, int b) {
    ivec2 transferred;
    transferred.y = b >> 1;
    transferred.y |= a & 0x80;
    transferred.x = a >> 1;
    transferred.x &= 0x3F;
    if ((transferred.x & 0x20) > 0) {
        transferred.x -= 0x40;
    }
    return transferred;
}

uvec4 ClampByte(ivec4 color) {
    return uvec4(clamp(color, 0, 255));
}

ivec4 BlueContract(int a, int r, int g, int b) {
    return ivec4(a, (r + b) >> 1, (g + b) >> 1, b);
}

void ComputeEndpoints(out uvec4 ep1, out uvec4 ep2, uint color_endpoint_mode, uint color_values[32],
                      inout uint colvals_index) {
#define READ_UINT_VALUES(N)                                                                        \
    uvec4 V[2];                                                                                    \
    for (uint i = 0; i < N; i++) {                                                                 \
        V[i / 4][i % 4] = color_values[++colvals_index];                      \
    }
#define READ_INT_VALUES(N)                                                                         \
    ivec4 V[2];                                                                                    \
    for (uint i = 0; i < N; i++) {                                                                 \
        V[i / 4][i % 4] = int(color_values[++colvals_index]);                      \
    }

    switch (color_endpoint_mode) {
    case 0: {
        READ_UINT_VALUES(2)
        ep1 = uvec4(0xFF, V[0].x, V[0].x, V[0].x);
        ep2 = uvec4(0xFF, V[0].y, V[0].y, V[0].y);
        break;
    }
    case 1: {
        READ_UINT_VALUES(2)
        const uint L0 = (V[0].x >> 2) | (V[0].y & 0xC0);
        const uint L1 = min(L0 + (V[0].y & 0x3F), 0xFFU);
        ep1 = uvec4(0xFF, L0, L0, L0);
        ep2 = uvec4(0xFF, L1, L1, L1);
        break;
    }
    case 4: {
        READ_UINT_VALUES(4)
        ep1 = uvec4(V[0].z, V[0].x, V[0].x, V[0].x);
        ep2 = uvec4(V[0].w, V[0].y, V[0].y, V[0].y);
        break;
    }
    case 5: {
        READ_INT_VALUES(4)
        ivec2 transferred = BitTransferSigned(V[0].y, V[0].x);
        V[0].y = transferred.x;
        V[0].x = transferred.y;
        transferred = BitTransferSigned(V[0].w, V[0].z);
        V[0].w = transferred.x;
        V[0].z = transferred.y;
        ep1 = ClampByte(ivec4(V[0].z, V[0].x, V[0].x, V[0].x));
        ep2 = ClampByte(ivec4(V[0].z + V[0].w, V[0].x + V[0].y, V[0].x + V[0].y, V[0].x + V[0].y));
        break;
    }
    case 6: {
        READ_UINT_VALUES(4)
        ep1 = uvec4(0xFF, (V[0].x * V[0].w) >> 8, (V[0].y * V[0].w) >> 8, (V[0].z * V[0].w) >> 8);
        ep2 = uvec4(0xFF, V[0].x, V[0].y, V[0].z);
        break;
    }
    case 8: {
        READ_UINT_VALUES(6)
        if ((V[0].y + V[0].w + V[1].y) >= (V[0].x + V[0].z + V[1].x)) {
            ep1 = uvec4(0xFF, V[0].x, V[0].z, V[1].x);
            ep2 = uvec4(0xFF, V[0].y, V[0].w, V[1].y);
        } else {
            ep1 = uvec4(BlueContract(0xFF, int(V[0].y), int(V[0].w), int(V[1].y)));
            ep2 = uvec4(BlueContract(0xFF, int(V[0].x), int(V[0].z), int(V[1].x)));
        }
        break;
    }
    case 9: {
        READ_INT_VALUES(6)
        ivec2 transferred = BitTransferSigned(V[0].y, V[0].x);
        V[0].y = transferred.x;
        V[0].x = transferred.y;
        transferred = BitTransferSigned(V[0].w, V[0].z);
        V[0].w = transferred.x;
        V[0].z = transferred.y;
        transferred = BitTransferSigned(V[1].y, V[1].x);
        V[1].y = transferred.x;
        V[1].x = transferred.y;
        if ((V[0].y + V[0].w + V[1].y) >= 0) {
            ep1 = ClampByte(ivec4(0xFF, V[0].x, V[0].z, V[1].x));
            ep2 = ClampByte(ivec4(0xFF, V[0].x + V[0].y, V[0].z + V[0].w, V[1].x + V[1].y));
        } else {
            ep1 = ClampByte(BlueContract(0xFF, V[0].x + V[0].y, V[0].z + V[0].w, V[1].x + V[1].y));
            ep2 = ClampByte(BlueContract(0xFF, V[0].x, V[0].z, V[1].x));
        }
        break;
    }
    case 10: {
        READ_UINT_VALUES(6)
        ep1 = uvec4(V[1].x, (V[0].x * V[0].w) >> 8, (V[0].y * V[0].w) >> 8, (V[0].z * V[0].w) >> 8);
        ep2 = uvec4(V[1].y, V[0].x, V[0].y, V[0].z);
        break;
    }
    case 12: {
        READ_UINT_VALUES(8)
        if ((V[0].y + V[0].w + V[1].y) >= (V[0].x + V[0].z + V[1].x)) {
            ep1 = uvec4(V[1].z, V[0].x, V[0].z, V[1].x);
            ep2 = uvec4(V[1].w, V[0].y, V[0].w, V[1].y);
        } else {
            ep1 = uvec4(BlueContract(int(V[1].w), int(V[0].y), int(V[0].w), int(V[1].y)));
            ep2 = uvec4(BlueContract(int(V[1].z), int(V[0].x), int(V[0].z), int(V[1].x)));
        }
        break;
    }
    case 13: {
        READ_INT_VALUES(8)
        ivec2 transferred = BitTransferSigned(V[0].y, V[0].x);
        V[0].y = transferred.x;
        V[0].x = transferred.y;
        transferred = BitTransferSigned(V[0].w, V[0].z);
        V[0].w = transferred.x;
        V[0].z = transferred.y;

        transferred = BitTransferSigned(V[1].y, V[1].x);
        V[1].y = transferred.x;
        V[1].x = transferred.y;

        transferred = BitTransferSigned(V[1].w, V[1].z);
        V[1].w = transferred.x;
        V[1].z = transferred.y;

        if ((V[0].y + V[0].w + V[1].y) >= 0) {
            ep1 = ClampByte(ivec4(V[1].z, V[0].x, V[0].z, V[1].x));
            ep2 = ClampByte(ivec4(V[1].w + V[1].z, V[0].x + V[0].y, V[0].z + V[0].w, V[1].x + V[1].y));
        } else {
            ep1 = ClampByte(BlueContract(V[1].z + V[1].w, V[0].x + V[0].y, V[0].z + V[0].w, V[1].x + V[1].y));
            ep2 = ClampByte(BlueContract(V[1].z, V[0].x, V[0].z, V[1].x));
        }
        break;
    }
    default: {
        // HDR mode, or more likely a bug computing the color_endpoint_mode
        ep1 = uvec4(0xFF, 0xFF, 0, 0);
        ep2 = uvec4(0xFF, 0xFF, 0, 0);
        break;
    }
    }
#undef READ_UINT_VALUES
#undef READ_INT_VALUES
}

uint UnquantizeTexelWeight(EncodingData val) {
    uint encoding = Encoding(val), bitlen = NumBits(val), bitval = BitValue(val);
    if (encoding == JUST_BITS) {
        return (bitlen >= 1 && bitlen <= 5)
            ? ((bitval * 64) + ((1 << bitlen) - 1) / 2) / ((1 << bitlen) - 1)
            : FastReplicateTo6(bitval, bitlen);
    } else if (encoding == TRIT || encoding == QUINT) {
        uint B = 0, C = 0, D = 0;
        uint b_mask = (0x3100 >> (bitlen * 4)) & 0xf;
        uint b = (bitval >> 1) & b_mask;
        D = QuintTritValue(val);
        if (encoding == TRIT) {
            switch (bitlen) {
            case 0: return D * 32; //0,32,64
            case 1: C = 50; break;
            case 2: C = 23; B = (b << 6) | (b << 2) | b; break;
            case 3: C = 11; B = (b << 5) | b; break;
            }
        } else if (encoding == QUINT) {
            switch (bitlen) {
            case 0: return D * 16; //0, 16, 32, 48, 64
            case 1: C = 28; break;
            case 2: C = 13; B = (b << 6) | (b << 1); break;
            }
        }
        uint A = ReplicateBitTo7(bitval & 1);
        uint res = (A & 0x20) | (((D * C + B) ^ A) >> 2);
        return res + (res > 32 ? 1 : 0);
    }
    return 0;
}

void UnquantizeTexelWeights(uvec2 size, bool is_dual_plane) {
    const uint num_planes = is_dual_plane ? 2 : 1;
    const uint area = size.x * size.y;
    const uint loop_count = min(result_index, area * num_planes);
    for (uint itr = 0; itr < loop_count; ++itr) {
        result_vector[itr] =
            UnquantizeTexelWeight(GetEncodingFromVector(itr));
    }
}

uint GetUnquantizedTexelWeight(uint offset_base, uint plane, bool is_dual_plane) {
    const uint offset = is_dual_plane ? 2 * offset_base + plane : offset_base;
    return result_vector[offset];
}

uvec4 GetUnquantizedWeightVector(uint t, uint s, uvec2 size, uint plane_index, bool is_dual_plane) {
    const uint Ds = uint((block_dims.x * 0.5f + 1024) / (block_dims.x - 1));
    const uint Dt = uint((block_dims.y * 0.5f + 1024) / (block_dims.y - 1));
    const uint area = size.x * size.y;

    const uint cs = Ds * s;
    const uint ct = Dt * t;
    const uint gs = (cs * (size.x - 1) + 32) >> 6;
    const uint gt = (ct * (size.y - 1) + 32) >> 6;
    const uint js = gs >> 4;
    const uint fs = gs & 0xF;
    const uint jt = gt >> 4;
    const uint ft = gt & 0x0F;
    const uint w11 = (fs * ft + 8) >> 4;
    const uint w10 = ft - w11;
    const uint w01 = fs - w11;
    const uint w00 = 16 - fs - ft + w11;
    const uvec4 w = uvec4(w00, w01, w10, w11);
    const uint v0 = jt * size.x + js;

    uvec4 p0 = uvec4(0);
    uvec4 p1 = uvec4(0);

    if (v0 < area) {
        const uint offset_base = v0;
        p0.x = GetUnquantizedTexelWeight(offset_base, 0, is_dual_plane);
        p1.x = GetUnquantizedTexelWeight(offset_base, 1, is_dual_plane);
    }
    if ((v0 + 1) < (area)) {
        const uint offset_base = v0 + 1;
        p0.y = GetUnquantizedTexelWeight(offset_base, 0, is_dual_plane);
        p1.y = GetUnquantizedTexelWeight(offset_base, 1, is_dual_plane);
    }
    if ((v0 + size.x) < (area)) {
        const uint offset_base = v0 + size.x;
        p0.z = GetUnquantizedTexelWeight(offset_base, 0, is_dual_plane);
        p1.z = GetUnquantizedTexelWeight(offset_base, 1, is_dual_plane);
    }
    if ((v0 + size.x + 1) < (area)) {
        const uint offset_base = v0 + size.x + 1;
        p0.w = GetUnquantizedTexelWeight(offset_base, 0, is_dual_plane);
        p1.w = GetUnquantizedTexelWeight(offset_base, 1, is_dual_plane);
    }

    const uint primary_weight = (uint(dot(p0, w)) + 8) >> 4;

    uvec4 weight_vec = uvec4(primary_weight);

    if (is_dual_plane) {
        const uint secondary_weight = (uint(dot(p1, w)) + 8) >> 4;
        for (uint c = 0; c < 4; c++) {
            const bool is_secondary = ((plane_index + 1u) & 3u) == c;
            weight_vec[c] = is_secondary ? secondary_weight : primary_weight;
        }
    }
    return weight_vec;
}

int FindLayout(uint mode) {
    if ((mode & 3) != 0) {
        if ((mode & 8) != 0) {
            if ((mode & 4) != 0) {
                if ((mode & 0x100) != 0) {
                    return 4;
                }
                return 3;
            }
            return 2;
        }
        if ((mode & 4) != 0) {
            return 1;
        }
        return 0;
    }
    if ((mode & 0x100) != 0) {
        if ((mode & 0x80) != 0) {
            if ((mode & 0x20) != 0) {
                return 8;
            }
            return 7;
        }
        return 9;
    }
    if ((mode & 0x80) != 0) {
        return 6;
    }
    return 5;
}


void FillError(ivec3 coord) {
    const uint total_texels = block_dims.x * block_dims.y;
    for (uint tid = gl_LocalInvocationIndex; tid < total_texels; tid += gl_WorkGroupSize.x * gl_WorkGroupSize.y) {
        uint x = tid % block_dims.x;
        uint y = tid / block_dims.x;
        imageStore(dest_image, coord + ivec3(x, y, 0), vec4(0.0, 0.0, 0.0, 0.0));
    }
}

void FillVoidExtentLDR(ivec3 coord) {
    // Thread 0 decodes color

    if (gl_LocalInvocationIndex == 0) {
        SkipBits(52);
        const uint r_u = StreamBits(16);
        const uint g_u = StreamBits(16);
        const uint b_u = StreamBits(16);
        const uint a_u = StreamBits(16);
        fill_color = vec4(float(r_u) / 65535.0f, float(g_u) / 65535.0f, float(b_u) / 65535.0f, float(a_u) / 65535.0f);
    }
    barrier();

    const uint total_texels = block_dims.x * block_dims.y;
    for (uint tid = gl_LocalInvocationIndex; tid < total_texels; tid += gl_WorkGroupSize.x * gl_WorkGroupSize.y) {
        uint x = tid % block_dims.x;
        uint y = tid / block_dims.x;
        imageStore(dest_image, coord + ivec3(x, y, 0), fill_color);
    }
}

bool IsError(uint mode) {
    if ((mode & 0x1ff) == 0x1fc) {
        if ((mode & 0x200) != 0) {
            // params.void_extent_hdr = true;
            return true;
        }
        if ((mode & 0x400) == 0 || StreamBits(1) == 0) {
            return true;
        }
        return false;
    }
    if ((mode & 0xf) == 0) {
        return true;
    }
    if ((mode & 3) == 0 && (mode & 0x1c0) == 0x1c0) {
        return true;
    }
    return false;
}

uvec2 DecodeBlockSize(uint mode) {
    uint A, B;
    switch (FindLayout(mode)) {
    case 0:
        A = (mode >> 5) & 0x3;
        B = (mode >> 7) & 0x3;
        return uvec2(B + 4, A + 2);
    case 1:
        A = (mode >> 5) & 0x3;
        B = (mode >> 7) & 0x3;
        return uvec2(B + 8, A + 2);
    case 2:
        A = (mode >> 5) & 0x3;
        B = (mode >> 7) & 0x3;
        return uvec2(A + 2, B + 8);
    case 3:
        A = (mode >> 5) & 0x3;
        B = (mode >> 7) & 0x1;
        return uvec2(A + 2, B + 6);
    case 4:
        A = (mode >> 5) & 0x3;
        B = (mode >> 7) & 0x1;
        return uvec2(B + 2, A + 2);
    case 5:
        A = (mode >> 5) & 0x3;
        return uvec2(12, A + 2);
    case 6:
        A = (mode >> 5) & 0x3;
        return uvec2(A + 2, 12);
    case 7:
        return uvec2(6, 10);
    case 8:
        return uvec2(10, 6);
    case 9:
        A = (mode >> 5) & 0x3;
        B = (mode >> 9) & 0x3;
        return uvec2(A + 6, B + 6);
    default:
        return uvec2(0);
    }
}

uint DecodeMaxWeight(uint mode) {
    const uint mode_layout = FindLayout(mode);
    uint weight_index = (mode & 0x10) != 0 ? 1 : 0;
    if (mode_layout < 5) {
        weight_index |= (mode & 0x3) << 1;
    } else {
        weight_index |= (mode & 0xc) >> 1;
    }
    weight_index -= 2;
    if ((mode_layout != 9) && ((mode & 0x200) != 0)) {
        weight_index += 6;
    }
    return weight_index + 1;
}

void DecompressBlock(ivec3 coord) {
    if (gl_LocalInvocationIndex == 0) {
        uint mode = StreamBits(11);
        bool early_exit = false;
        if (IsError(mode)) {
            size_params = uvec2(0);
            early_exit = true;
        } else if ((mode & 0x1ff) == 0x1fc) {
            size_params = uvec2(0xFFFFFFFF);
            early_exit = true;
        } else {
            size_params = DecodeBlockSize(mode);
            if ((size_params.x > block_dims.x) || (size_params.y > block_dims.y)) {
               size_params = uvec2(0);
               early_exit = true;
            }
        }

        if (!early_exit) {
            num_partitions = StreamBits(2) + 1;
            uint mode_layout = FindLayout(mode);
            dual_plane = (mode_layout != 9) && ((mode & 0x400) != 0);
            if (num_partitions > 4 || (num_partitions == 4 && dual_plane)) {
                size_params = uvec2(0);
                early_exit = true;
            }
        }

        if (!early_exit) {
            uint partition_index_local = 1;
            uvec4 color_endpoint_mode = uvec4(0);
            uint ced_pointer = 0;
            uint base_cem = 0;
            if (num_partitions == 1) {
                color_endpoint_mode.x = StreamBits(4);
                partition_index_local = 0;
            } else {
                partition_index_local = StreamBits(10);
                base_cem = StreamBits(6);
            }
            partition_index = partition_index_local; // Store to shared
            const uint base_mode = base_cem & 3;
            const uint max_weight = DecodeMaxWeight(mode);
            const uint weight_bits = GetPackedBitSize(size_params, dual_plane, max_weight);
            uint remaining_bits = 128 - weight_bits - total_bitsread;
            uint extra_cem_bits = 0;
            if (base_mode > 0) {
                switch (num_partitions) {
                case 2: extra_cem_bits += 2; break;
                case 3: extra_cem_bits += 5; break;
                case 4: extra_cem_bits += 8; break;
                }
            }
            remaining_bits -= extra_cem_bits;
            const uint plane_selector_bits = dual_plane ? 2 : 0;
            remaining_bits -= plane_selector_bits;
            if (remaining_bits > 128) {
                size_params = uvec2(0); // Error
            } else {
                const uint color_data_bits = remaining_bits;
                while (remaining_bits > 0) {
                    const int nb = int(min(remaining_bits, 32U));
                    const uint b = StreamBits(nb);
                    color_endpoint_data[ced_pointer] = uint(bitfieldExtract(b, 0, nb));
                    ++ced_pointer;
                    remaining_bits -= nb;
                }
                plane_index = uint(StreamBits(plane_selector_bits));
                if (base_mode > 0) {
                    const uint extra_cem = StreamBits(extra_cem_bits);
                    uint cem = (extra_cem << 6) | base_cem;
                    cem >>= 2;
                    uvec4 C = uvec4(0);
                    for (uint i = 0; i < num_partitions; i++) {
                        C[i] = (cem & 1); cem >>= 1;
                    }
                    uvec4 M = uvec4(0);
                    for (uint i = 0; i < num_partitions; i++) {
                        M[i] = cem & 3; cem >>= 2;
                    }
                    for (uint i = 0; i < num_partitions; i++) {
                        color_endpoint_mode[i] = base_mode;
                        if (C[i] == 0) --color_endpoint_mode[i];
                        color_endpoint_mode[i] <<= 2;
                        color_endpoint_mode[i] |= M[i];
                    }
                } else if (num_partitions > 1) {
                    const uint cem = base_cem >> 2;
                    for (uint i = 0; i < num_partitions; i++) {
                        color_endpoint_mode[i] = cem;
                    }
                }

                result_limit_reached = false;
                uint colvals_index = 0;
                DecodeColorValues(color_endpoint_mode, num_partitions, color_data_bits);
                for (uint i = 0; i < num_partitions; i++) {
                    ComputeEndpoints(endpoints0[i], endpoints1[i], color_endpoint_mode[i], color_values_direct, colvals_index);
                }

                color_endpoint_data = local_buff;
                color_endpoint_data = bitfieldReverse(color_endpoint_data).wzyx;
                const uint clear_byte_start = (weight_bits >> 3) + 1;
                const uint byte_insert = ExtractBits(color_endpoint_data, int(clear_byte_start - 1) * 8, 8) & uint(((1 << (weight_bits % 8)) - 1));
                const uint vec_index = (clear_byte_start - 1) >> 2;
                color_endpoint_data[vec_index] = bitfieldInsert(color_endpoint_data[vec_index], byte_insert, int((clear_byte_start - 1) % 4) * 8, 8);
                for (uint i = clear_byte_start; i < 16; ++i) {
                    const uint idx = i >> 2;
                    color_endpoint_data[idx] = bitfieldInsert(color_endpoint_data[idx], 0, int(i % 4) * 8, 8);
                }

                result_index = 0;
                color_bitsread = 0;
                result_limit_reached = false;
                result_vector_max_index = size_params.x * size_params.y;
                if (dual_plane) result_vector_max_index *= 2;
                DecodeIntegerSequence(max_weight, GetNumWeightValues(size_params, dual_plane));
                UnquantizeTexelWeights(size_params, dual_plane);

                if (num_partitions > 1) {
                    pt = GetPartitionTable(partition_index, num_partitions);
                }
            }
        }
    }
    barrier();

    if (size_params.x == 0) {
        FillError(coord);
        return;
    }
    if (size_params.x == 0xFFFFFFFF) {
        FillVoidExtentLDR(coord);
        return;
    }

    const uint total_texels = block_dims.x * block_dims.y;
    for (uint tid = gl_LocalInvocationIndex; tid < total_texels; tid += gl_WorkGroupSize.x * gl_WorkGroupSize.y) {
        uint x = tid % block_dims.x;
        uint y = tid / block_dims.x;

        uint local_partition = 0;
        if (num_partitions > 1) {
            local_partition = SelectPartition(pt, x, y, num_partitions);
        }
        const uvec4 C0 = ReplicateByteTo16(endpoints0[local_partition]);
        const uvec4 C1 = ReplicateByteTo16(endpoints1[local_partition]);
        const uvec4 weight_vec = GetUnquantizedWeightVector(y, x, size_params, plane_index, dual_plane);
        const vec4 Cf = vec4((C0 * (uvec4(64) - weight_vec) + C1 * weight_vec + uvec4(32)) / 64);
        const vec4 p = (Cf / 65535.0f);
        imageStore(dest_image, coord + ivec3(x, y, 0), p.gbar);
    }
}

uint SwizzleOffset(uvec2 pos) {
    return ((pos.x & 32u) << 3u) |
           ((pos.y & 6u)  << 5u) |
           ((pos.x & 16u) << 1u) |
           ((pos.y & 1u)  << 4u) |
           (pos.x & 15u);
}

void main() {
    uvec3 block_id = gl_WorkGroupID;
    uvec3 pos = block_id;
    pos.x <<= BYTES_PER_BLOCK_LOG2;
    const uint swizzle = SwizzleOffset(pos.xy);
    const uint block_y = pos.y >> GOB_SIZE_Y_SHIFT;

    uint offset = 0;
    offset += pos.z * layer_stride;
    offset += (block_y >> block_height) * block_size;
    offset += (block_y & block_height_mask) << GOB_SIZE_SHIFT;
    offset += (pos.x >> GOB_SIZE_X_SHIFT) << x_shift;
    offset += swizzle;

    if (gl_LocalInvocationIndex == 0) {
       total_bitsread = 0;
       result_index = 0;
       color_bitsread = 0;
       write_color_values = false;
       result_limit_reached = false;
       color_out_index = 0;
       color_num_values = 0;
       local_buff = astc_data[offset / 16];
    }
    barrier();

    ivec3 coord = ivec3(block_id * uvec3(block_dims, 1));
    if (any(greaterThanEqual(coord, imageSize(dest_image)))) {
        return;
    }
    DecompressBlock(coord);
}
